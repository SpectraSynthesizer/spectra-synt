 
spec WashPL

define NUM_MODES := 5;

env boolean startWash; // Wash
env boolean startDry; // Dry
env boolean setHot; // Heat
env boolean setDelay; // Delay

sys boolean atTemp; // Heat
sys boolean locked; // True
sys boolean[NUM_MODES] modes; // Domain-Knowledge
	define idle := modes[0];
	define washing := modes[1];
	define drying := modes[2];
	define heating := modes[3];
	define waiting := modes[4];

-- Universal Assumptions

-- Feature-level Assumptions
asm WashPressedLive: alwEv(startWash); // Wash
asm WashNotPressedLive: alwEv(!startWash);  // Wash
asm DryNotPressedLive: alwEv(!startDry); // Dry

-- Universal Guarantees
gar ModeMutex : true;

-- Feature-level Guarantees
gar WashButtonSafety: alw(startWash and idle implies next(washing));  // Wash

gar WashOffLive: alwEv(!washing); // Wash
gar WashLockSafety: alw(washing implies locked);  // Wash

gar DryOffLive: alwEv(!drying); // Dry
gar DryButtonSafety: alw(startDry and idle implies next(drying)); // Dry
gar DryLockSafety: alw(drying implies locked);

gar HotButtonSafety: alw(setHot implies next(heating)); //Heat
gar HeatingOnIffSafety: alw(next(heating) iff ((heating and !atTemp) or setHot)); // Heat

gar AtTempSafety: alw(atTemp implies next(washing)); // Heat
gar HeatingLockSafety: alw(heating implies locked); // Heat
gar AtTempLockSafety: alw(atTemp implies locked); // Heat

gar WaitingWashSafety: alw(startWash and !waiting implies next(washing)); // Delay
gar DelayButtonSafety: alw(setDelay and !washing implies next(waiting));
gar WaitingSafety: alw((waiting and next(!waiting)) implies next(washing));

gar modes_mutex:
	alw(forall i in Int(0..(NUM_MODES-1)) . forall j in Int(0..(NUM_MODES-1)) . (i!=j) -> !(modes[i] & modes[j]));

@feature Wash {
	guarantees: WashButtonSafety, WashOffLive, WashLockSafety
	assumptions: WashPressedLive, WashNotPressedLive
}

@feature Dry {
	guarantees: DryButtonSafety, DryOffLive, DryLockSafety
	assumptions: DryNotPressedLive
}

@feature Heat {
	guarantees: HotButtonSafety, HeatingOnIffSafety, AtTempSafety, HeatingLockSafety, AtTempLockSafety
}

@feature Delay {
	guarantees: WaitingWashSafety,DelayButtonSafety,WaitingSafety
}

@featuremodel Wash & !(Heat & Delay);


